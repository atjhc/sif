(--
Solution to part 1 of https://adventofcode.com/2024/day/8
--)

set board to []
repeat for line in the list of lines in the contents of file "input.txt"
  insert the list of characters in line at the end of board
end repeat
set height to the size of board
set width to the size of board[0]

set antennas to [:]
repeat for y in 0 ..< height
  repeat for x in 0 ..< width
    set value to board[y][x]
    if value != "." then
      if antennas contains value = no then
        set antennas[value] to []
      end if
      insert ["x": x, "y": y] at the end of antennas[value]
    end if
  end repeat
end repeat

function calculate antinodes {firstX, firstY, secondX, secondY}
  set differenceX to the abs of (firstX - secondX)
  if firstX < secondX then
    set firstAntinodeX to firstX - differenceX
    set secondAntinodeX to secondX + differenceX
  else
    set firstAntinodeX to firstX + differenceX
    set secondAntinodeX to secondX - differenceX
  end if

  set differenceY to the abs of (firstY - secondY)
  if firstY < secondY then
    set firstAntinodeY to firstY - differenceY
    set secondAntinodeY to secondY + differenceY
  else
    set firstAntinodeY to firstY + differenceY
    set secondAntinodeY to secondY - differenceY
  end if

  return [
    ["x": firstAntinodeX, "y": firstAntinodeY],
    ["x": secondAntinodeX, "y": secondAntinodeY]
  ]
end function

function within bounds {x, y}
  return x >= 0 and x < width and y >= 0 and y < height
end function

repeat for antennaType in the keys of antennas
  set locations to antennas[antennaType]
  if the size of locations = 1 then next repeat

  repeat for i in 0 ..< (the size of locations) - 1
    set first to locations[i]
    repeat for j in (i + 1) ..< the size of locations
      set second to locations[j]
      set antinodeList to calculate antinodes(first["x"], first["y"], second["x"], second["y"])
      set antinode1 to item 0 in antinodeList
      set antinode2 to item 1 in antinodeList

      if within bounds(antinode1["x"], antinode1["y"]) then
        set board[antinode1["y"]][antinode1["x"]] to "#"
      end if
      if within bounds(antinode2["x"], antinode2["y"]) then
        set board[antinode2["y"]][antinode2["x"]] to "#"
      end if
    end repeat
  end repeat
end repeat

set count to 0
repeat for row in board
  repeat for value in row
    if value = "#" then
      set count to count + 1
    end if
  end repeat
end repeat

print count
(-- expect
247
--)
