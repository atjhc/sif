-- http://rosettacode.org/wiki/CSV_to_HTML_translation

function replace all (src: string) with (dst: string) in (text: string) -> string
  set result to empty
  repeat
    set index to the offset of src in text
    if index is empty then
      put text after result
      exit repeat
    end if
    
    put char 1 to (index - 1) of text after result
    put dst after result
    delete char 1 to (index + length(src) - 1) of text
  end repeat
  return result
end function

function html of (csv: string)
  put "<table>\n" into html
  repeat while csv <> empty
    put line 1 of csv into row
    put "<tr>" after html
    repeat while row is not empty
      put item 1 of row into theItem
      set theItem to replace all "&" with "&amp;" in theItem
      set theItem to replace all "<" with "&lt;" in theItem
      set theItem to replace all ">" with "&gt;" in theItem
      put "<td>" & theItem & "</td>" after html
      delete item 1 of row
    end repeat
    put "</tr>\n" after html
    delete line 1 of csv
  end repeat
  put "</table>\n" after html
  return html
end function

set csv to "Character,Speech
The multitude,The messiah! Show us the messiah!
Brians mother,<angry>Now you listen here! He's not the messiah; he's a very naughty boy! Now go away!</angry>
The multitude,Who are you?
Brians mother,I'm his mother; that's who!
The multitude,Behold his mother! Behold his mother!"

print line html of csv
