-- Print a line of text to stdout using the "print" function:
print "Hello, World!"

-- Print text without a newline, by using the "write" function:
write "Type something: "

-- Read a line of text (note: "a" is optional):
read a line

-- The value is automatically captured into the "it" variable:
print it

-- Though less idiomatic, you may also compose these functions:
write "Type something: "
print read line

-- Set a variable using the "set" command:
set firstName to "Hari"

-- Variable names may use non-ASCII characters:
set 名前 to "Hari"

-- "if" statements can be a single line:
if firstName = "Hari" then set lastName to "Seldon"

-- Or span multiple lines as a block:
if firstName = "Hari" then
  set lastName to "Seldon"
  set nickname to "Raven"
end if

-- You can print multiple things at once:
print "Welcome,", firstName, nickname, lastName

-- Use "repeat" to loop over code:
print "Ask a question: "
repeat
  write "> "
  read a line
  set input to it
  if input = empty then exit repeat
  else print "I don't understand", input
end repeat

-- Functions may have multiple words in their name:
function the password
  return "12345"
end function

write "Password: "
read a line
if it = the password then print "Correct"
else print "Incorrect, but trusting you for now..."
