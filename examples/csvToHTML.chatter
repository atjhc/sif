-- http://rosettacode.org/wiki/CSV_to_HTML_translation

on begin
  put "Character,Speech
The multitude,The messiah! Show us the messiah!
Brians mother,<angry>Now you listen here! He's not the messiah; he's a very naughty boy! Now go away!</angry>
The multitude,Who are you?
Brians mother,I'm his mother; that's who!
The multitude,Behold his mother! Behold his mother!" into csv
  put csvToHTML(csv)
end begin

function csvToHTML csv
  put "<table>\n" into html
  repeat while csv <> empty
    put line 1 of csv into row
    put "<tr>" after html
    repeat while row is not empty
      put item 1 of row into theItem
      put replaceAll(theItem, "&", "&amp;") into theItem
      put replaceAll(theItem, "<", "&lt;") into theItem
      put replaceAll(theItem, ">", "&gt;") into theItem
      put "<td>" & theItem & "</td>" after html
      delete item 1 of row
    end repeat
    put "</tr>\n" after html
    delete line 1 of csv
  end repeat
  put "</table>\n" after html
  return html
end csvToHTML

function replaceAll text, src, dst
  put empty into result
  repeat
    put offset(src, text) into index
    if index is 0 then
      put text after result
      exit repeat
    end if
    
    put char 1 to (index - 1) of text after result
    put dst after result
    delete char 1 to (index + length(src) - 1) of text
  end repeat
  return result
end replaceAll
